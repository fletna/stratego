module static-semantics

signature
  sorts Section SigSection Module OpDecl Sort Rule Term Type Def RuleCond
        ConstType Strategy SVar
  constructors
    Module       : string * list(Section) -> Module
    Signature    : list(SigSection) -> Section
    Rules        : list(Def) -> Section
    
    Sorts        : list(Sort) -> SigSection
    Constructors : list(OpDecl) -> SigSection
    OpDecl       : string * Type -> OpDecl
    
    FunType      : list(Type) * Type -> Type
    ConstType    : Sort -> Type
    
    SortNoArgs   : string -> Sort
    RDefNoArgs   : string * Rule -> Def
    SDefNoArgs   : string * Strategy -> Def
    SDefTypeCheck: string * Strategy -> Def
    Rule         : Term * Term * list(RuleCond) -> Rule
    
    WhereClause  : Strategy -> RuleCond
    WithClause   : Strategy -> RuleCond
    
    BA           : Strategy * Term -> Strategy
    CallNoArgs   : SVar -> Strategy
   
    SVar         : string -> SVar
    
    CastTerm     : Term * TYPE * TYPE -> Term
    NoAnnoList   : Term -> Term
    Var          : string -> Term
    Tuple		 : list(Term) -> Term
    Op           : string * list(Term) -> Term
    App			 : Strategy * Term -> Term
    Seq			 : Strategy * Strategy -> Strategy
    Assign       : Term * Term -> Strategy
    Match        : Term -> Strategy
    Id           : Term
    Wld          : Term
    
  sorts TYPE
  constructors
    SRT : string -> TYPE
    FUN : list(TYPE) * TYPE -> TYPE
    TUP : list(TYPE) -> TYPE
    DYN : TYPE
    CAST: TYPE * TYPE -> TYPE
  
  
  namespaces
    Sort : string
    Cons : string
    Var : string
    Rule : string
    
  relations
    typeOfSort     : occurrence -> TYPE
    typeOfCons     : occurrence -> TYPE
    typeOfVar      : occurrence -> TYPE
    typeOfTerm     : occurrence -> TYPE
    typeOfRule 	   : occurrence -> TYPE
    typeOfStrategy : occurrence -> TYPE
    
  name-resolution 
    labels P

rules 

  insertCasts : Module -> Module
  insertCasts(m) = m' :- moduleOkC(m) == m'.
  
  consistent: TYPE * TYPE
  consistent(DYN(), T).
  consistent(T, DYN).
  
  consistentL maps consistent(list(*), list(*))
  
  cast: Term * TYPE * TYPE -> Term
  cast(e, T, T) = e.
  cast(e, T1, T2) = CastTerm(e, T1, T2).
    
rules moduleOk : Module 
 
  moduleOk(m) :- {m'} moduleOkC(m) == m'.

  moduleOkC : Module -> Module

  moduleOkC(Module(x, sections)) = Module(x, sections'') :- {s typechecks sections'}
    new s,
    sectionsOk(s, sections) == sections''.//,
//    insertTypeCheckSections(s, sections) == typechecks,
//    sections'' == concatenate(sections', [typechecks]).

  concatenate : list(Section) * list(Section) -> list(Section)
  concatenate([s], s2) = s3 :- s3 == [s|s2].
  concatenate([s|s1], s2) = s3 :- s3 == [s|concatenate(s1, s2)].
  

rules 

  sectionOk  : scope * Section -> Section
  sectionsOk maps sectionOk(*, list(*)) = list(*)
  

rules // signatures
  
  sectionOk(s, Signature(sigsections)) = Signature(sigsections') :-
    sigSectionsOk(s, sigsections) == sigsections'.
  
  sigSectionOk: scope * SigSection -> SigSection
  sigSectionsOk maps sigSectionOk(*, list(*)) = list(*)
  
  sigSectionOk(s, Sorts(srts)) = Sorts(srts) :- 
    sortsOk(s, srts).
  
  sortOk : scope * Sort 
  sortsOk maps sortOk(*, list(*)) 
  
  sortOk(s, SortNoArgs(srt)) :- {d}
    d == Sort{srt@srt},
    s -> d with typeOfSort SRT(srt).
  
  sigSectionOk(s, Constructors(cdecls)) = Constructors(cdecls) :- 
    consDeclsOk(s, cdecls).
    
  consDeclOk : scope * OpDecl
  consDeclsOk maps consDeclOk(*, list(*))
  
  consDeclOk(s, OpDecl(c, FunType(srts, srt))) :- {ts t}
    typeOfSorts(s, srts) == ts,
    typeOfSort(s, srt) == t,
    s -> Cons{c@c} with typeOfCons FUN(ts, t).
    
  consDeclOk(s, OpDecl(c, ConstType(srt))) :- {t}
  	typeOfSort(s, ConstType(srt)) == t,
  	s -> Cons{c@c} with typeOfCons FUN([], t).
  
  typeOfSort : scope * Type -> TYPE
  typeOfSorts maps typeOfSort(*, list(*)) = list(*)
  
  typeOfSort(s, ConstType(SortNoArgs(srt))) = t :- {p d}
    typeOfSort of Sort{srt@srt} in s |-> [(p, (d, t))].
  
rules // rules 

  sectionOk(s, Rules(rs)) = Rules(rs') :- 
    rulesOk(s, rs) == rs'.
  
  ruleOk : scope * Def -> Def
  rulesOk maps ruleOk(*, list(*)) = list(*)
  
  ruleOk(s, SDefNoArgs(f, rules)) = SDefNoArgs(f, rules).
  ruleOk(s, SDefTypeCheck(f, rules)) = SDefTypeCheck(f, rules).
  
  ruleOk(s, RDefNoArgs(f, Rule(trm1, trm2, cond))) = RDefNoArgs(f, Rule(trm1, trm2', cond'))  :- {s_r t1 t2}
    new s_r, s_r -P-> s,
    typeOfMatchTerm(s_r, trm1) == t1,
    typeOfBuildTerm(s_r, trm2) == (trm2', t2),
    checkConds(s_r, cond) == cond',
    s -> Rule{f@f} with typeOfRule FUN([t1], t2).
   
  checkCond: scope * RuleCond -> RuleCond
  checkConds maps checkCond(*, list(*)) = list(*)
  
  insertCast : scope * RuleCond -> RuleCond
  insertCast(s, cond) = cond' :-
    WithClause(BA(CallNoArgs(SVar("is-string")), Var("x"))) == cond'.
  
  checkCond(s, WhereClause(str)) = WhereClause(str') :-
  	checkStrategy(s, str) == str'.
  	  	
  checkCond(s, WithClause(str)) = WithClause(str') :-
  	checkStrategy(s, str) == str'.
  
  checkStrategy: scope * Strategy -> Strategy
  
  typeOfStrategy: scope * Strategy -> TYPE
  
  typeOfStrategy(s, Seq(str1, str2)) = FUN([t11], t22) :- {t12 t21}
  	typeOfStrategy(s, str1) == FUN([t11], t12),
  	typeOfStrategy(s, str2) == FUN([t21], t22),
  	t12 == t21.
  	
  typeOfStrategy(s, CallNoArgs(SVar(str))) = t :- {p d t'}
  	typeOfRule of Rule{str@str} in s |-> [(p, (d, t'))],
  	t == t'.
  	
  checkStrategy(s, Seq(str1,str2)) = Seq(str1, str2) :- {t1 t2}
  	checkStrategy(s, str1) == t1,
  	checkStrategy(s, str2) == t2.
  
  checkStrategy(s, Assign(Var(x), sth)) = Assign(Var(x), sth) :- {t trm}
  	typeOfBuildTerm(s, sth) == (trm, t),
  	s -> Var{x@x} with typeOfVar t.
  	//typeOfVar of Var{x@x} in s |-> [(p, (d, t))],
  
  typeOfTerm: scope * Term -> TYPE
  
  typeOfMatchTerm : scope * Term -> TYPE
  typeOfMatchTerms maps typeOfMatchTerm(*, list(*)) = list(*)
  
  typeOfMatchTerm(s, NoAnnoList(Var(x))) = DYN() :-
    s -> Var{x@x} with typeOfVar DYN().  
  
  typeOfMatchTerm(s, NoAnnoList(term)) = t :-
    typeOfMatchTerm(s, term) == t.
  
  typeOfMatchTerm(s, Var(x)) = t :-
   s -> Var{x@x} with typeOfVar t,
   t == DYN().
  
  typeOfMatchTerm(s, Op(c, trms)) = t :- {p d ts ts'}
    typeOfCons of Cons{c@c} in s |-> [(p, (d, FUN(ts, t)))],
    typeOfMatchTerms(s, trms) == ts', consistentL(ts, ts').
   
  typeOfMatchTerm(s, Tuple(trms)) = TUP(ts) :- 
  	typeOfMatchTerms(s, trms) == ts.
  	
  typeOfBuildTerm : scope * Term -> (Term * TYPE)
  typeOfBuildTerms: scope * list(Term) -> (list(Term) * list(TYPE))
  
  
  typeOfBuildTerms(s, [term]) = ([trm], [ty]) :-
  	typeOfBuildTerm(s, term) == (trm, ty).
  	
  typeOfBuildTerms(s, [hd|terms]) = ([hd'|terms'], [t|types]) :-
    typeOfBuildTerm(s, hd) == (hd', t),
    typeOfBuildTerms(s, terms) == (terms', types).
  
  
  typeOfBuildTerm(s, NoAnnoList(term)) = (NoAnnoList(term'), t) :-
    typeOfBuildTerm(s, term) == (term', t).
    
  typeOfBuildTerm(s, App(CallNoArgs(SVar(str)), term)) = t :- {p d t1 t2}
  	typeOfRule of Rule{str@str} in s |-> [(p, (d, FUN([t1], t2)))],
  	typeOfBuildTerm(s, term) == (term, t1).
  	
  typeOfBuildTerm(s, App(Seq(str1, str2), term)) = (term', t') :- {t}
  	typeOfStrategy(s, Seq(str1, str2)) == FUN([t], t'),
  	typeOfBuildTerm(s, term) == (term', t).
  
  typeOfBuildTerm(s, Var(x)) = (trm, t) :- {p d t'}
   typeOfVar of Var{x@x} in s |-> [(p, (d, t'))],
   cast(Var(x), t', t) == trm.

  typeOfBuildTerm(s, Op(c, trms)) = (Op(c, trms'), t) :- {p d ts}
    typeOfCons of Cons{c@c} in s |-> [(p, (d, FUN(ts, t)))],
    typeOfBuildTerms(s, trms) == (trms', ts).
    
  typeOfBuildTerm(s, CastTerm(e, t1, t2)) = (CastTerm(e, t1, t2), t). // Silence CastTerm
    
rules // insertTypeChecks

  insertTypeCheckSection : scope * Section -> Section
  insertTypeCheckSections maps insertTypeCheckSection(*, list(*)) = *
  
  insertTypeCheckSection(s, Signature(sigSec)) = Rules(rs) :-
    insertTypeCheckSigSections(s, sigSec) == rs.
  
  insertTypeCheckSection(s, Rules(rs)) = nil.
  
  insertTypeCheckSigSection : scope * SigSection -> list(Def)
  insertTypeCheckSigSections maps insertTypeCheckSigSection(*, list(*)) = *
  
  insertTypeCheckSigSection(s, Sorts(sorts)) = nil.
  insertTypeCheckSigSection(s, Constructors(cons)) = def :-
    insertTypeCheckCons(s, cons) == def.
    
  insertTypeCheckCon : scope * OpDecl -> Def
  insertTypeCheckCons maps insertTypeCheckCon(*, list(*)) = list(*)
  
  insertTypeCheckCon(s, OpDecl(c, FunType(srts, ConstType(SortNoArgs(typ))))) = strat :- {args}
    toWlds(srts) == args,
    strat == SDefTypeCheck(typ, Match(NoAnnoList(Op(c, args)))).

  toWld : Type -> Term
  toWlds maps toWld(list(*)) = list(*)
  toWld(ConstType(SortNoArgs(srt))) = Wld().
