module static-semantics

signature
  sorts Section SigSection Module OpDecl Sort Rule Term Type Def RuleCond
        ConstType Strategy SVar Typedid
  constructors
    Module       : string * list(Section) -> Module
    Signature    : list(SigSection) -> Section
    Rules        : list(Def) -> Section
    Strategies   : list(Def) -> Section
    
    Sorts        : list(Sort) -> SigSection
    Constructors : list(OpDecl) -> SigSection
    OpDecl       : string * Type -> OpDecl
    
    FunType      : list(Type) * Type -> Type
    ConstType    : Sort -> Type
    
    SortNoArgs   : string -> Sort
    Sort         : string * list(Sort) -> Sort
    SortVar      : string -> Sort
    RDefNoArgs   : string * Rule -> Def
    SDefNoArgs   : string * Strategy -> Def
    SDefTypeCheck: string * string * list(Type) -> Def

    RDecl	     : string * TYPE * TYPE -> Def
    RDeclT		 : string * list(TYPE) * list(TYPE) * TYPE -> Def
    ExtSDefInl   : string * list(Typedid) * list(Typedid) * Strategy -> Def
    AnnoDef      : list(string) * Def -> Def
    RDef         : string * list(Typedid) * Rule -> Def
    RDefT        : string * list(Typedid) * list(Typedid) * Rule -> Def
    SDefT        : string * list(Typedid) * list(Typedid) * Strategy -> Def
    
    VarDec       : string * Type -> Typedid
    Rule         : Term * Term * list(RuleCond) -> Rule
    
    WhereClause  : Strategy -> RuleCond
    WithClause   : Strategy -> RuleCond
    
    BA           : Strategy * Term -> Strategy
    CallNoArgs   : SVar -> Strategy
    CastValStrat : Def
   
    SVar         : string -> SVar
    
    // Term
    CastTerm     : Term * TYPE * TYPE -> Term
    OfType       : Term * TYPE -> Term
    BuildDefault : Term -> Term
  	AnnoList     : Term * Term -> Term
    NoAnnoList   : Term -> Term
    App			 : Strategy * Term -> Term
    RootApp      : Strategy -> Term
    
    Char         : string -> Term
    Var          : string -> Term
    Tuple		 : list(Term) -> Term
    List         : list(Term) -> Term
    ListTail     : list(Term) * Term -> Term
    BuildDefaultPT : Term -> Term
    Int          : string -> Term
    Real         : string -> Term
    Str          : string -> Term
    Op           : string * list(Term) -> Term
    OpQ          : string * list(Term) -> Term
    Explode      : Term * Term -> Term
    As           : string * Term -> Term
    Wld          : Term

    // Strategy
    Seq			 : Strategy * Strategy -> Strategy
    Assign       : Term * Term -> Strategy
    Match        : Term -> Strategy
    Build        : Term -> Strategy
    Id           : Strategy

    
  sorts TYPE
  constructors
    SRT : string -> TYPE
    SRT_VAR : string -> TYPE
    STRING : TYPE
    INT : TYPE
    REAL : TYPE
    FUN : list(TYPE) * TYPE -> TYPE
    TUP : list(TYPE) -> TYPE
    LIST : TYPE -> TYPE
    DYN : TYPE
    CAST: TYPE * TYPE -> TYPE
  
  
  namespaces
    Sort : string
    Cons : string
    Var : string
    Rule : string
    
  relations
    typeOfSort     : occurrence -> TYPE
    typeOfCons     : occurrence -> TYPE
    typeOfVar      : occurrence -> TYPE
    typeOfTerm     : occurrence -> TYPE
    typeOfRule 	   : occurrence -> TYPE
    typeOfStrategy : occurrence -> TYPE
    
  name-resolution 
    labels P

rules 

  insertCasts : Module -> Module
  insertCasts(m) = m' :- moduleOkC(m) == m'.
  
  insertTypeChecks : Module -> Module
  insertTypeChecks(Module(x, sections)) = Module(x, sections') :- {typechecks}
    insertTypeCheckSections(sections) == typechecks,
    sections' == concatenate(sections, [typechecks]).
    
  consistent: TYPE * TYPE
  consistent(DYN(), T).
  consistent(T, DYN()).
  
  consistentL maps consistent(list(*), list(*))
  
  cast: Term * TYPE * TYPE -> Term
  cast(e, T, T) = e.
  cast(e, T1, T2) = OfType(e, T2) :- consistent(T1, T2).
    
rules moduleOk : Module 
 
  moduleOk(m) :- {m'} moduleOkC(m) == m'.

  moduleOkC : Module -> Module

  moduleOkC(Module(x, sections)) = Module(x, sections') :- {s}
    new s,
    sectionsOk(s, sections) == sections'.

  concatenate : list(Section) * list(Section) -> list(Section)
  concatenate([s], s2) = s3 :- s3 == [s|s2].
  concatenate([s|s1], s2) = s3 :- s3 == [s|concatenate(s1, s2)].
  

rules 

  sectionOk  : scope * Section -> Section
  sectionsOk maps sectionOk(*, list(*)) = list(*)
  

rules // signatures
  
  sectionOk(s, Signature(sigsections)) = Signature(sigsections') :-
    sigSectionsOk(s, sigsections) == sigsections'.
  
  sigSectionOk: scope * SigSection -> SigSection
  sigSectionsOk maps sigSectionOk(*, list(*)) = list(*)
  
  sigSectionOk(s, Sorts(srts)) = Sorts(srts) :- 
    sortsOk(s, srts).
  
rules // sorts
  sortOk : scope * Sort 
  sortsOk maps sortOk(*, list(*)) 
  
  sortOk(s, SortNoArgs(srt)) :- {d}
    d == Sort{srt@srt},
    s -> d with typeOfSort SRT(srt).
  
  /*
  sortOk(s, Sort(srt, vars)) :- {d var_t}
    d == Sort{srt@srt},
    s -> d with typeOfSort SRT(srt),
    typeOfSorts(s, vars) == var_t.
  sortOk(s, SortVar(x)).
  */
  
  sigSectionOk(s, Constructors(cdecls)) = Constructors(cdecls) :- 
    consDeclsOk(s, cdecls).
    
  consDeclOk : scope * OpDecl
  consDeclsOk maps consDeclOk(*, list(*))
  
  consDeclOk(s, OpDecl(c, FunType(srts, srt))) :- {ts t}
    typeOfTypes(s, srts) == ts,
    typeOfType(s, srt) == t,
    s -> Cons{c@c} with typeOfCons FUN(ts, t).
    
  consDeclOk(s, OpDecl(c, ConstType(srt))) :- {t}
  	typeOfSort(s, srt) == t,
  	s -> Cons{c@c} with typeOfCons FUN([], t).
  
  typeOfType : scope * Type -> TYPE
  typeOfTypes maps typeOfType(*, list(*)) = list(*)
  
  typeOfType(s, ConstType(srt)) = typeOfSort(s, srt).
  
  typeOfSort : scope * Sort -> TYPE
  typeOfSorts maps typeOfSort(*, list(*)) = list(*)
  
  typeOfSort(s, SortNoArgs(srt)) = t :- {p d}
    typeOfSort of Sort{srt@srt} in s |-> [(p, (d, t))].
    
  typeOfSort(s, Sort(srt, args)) = t :- {p d}
    typeOfSort of Sort{srt@srt} in s |-> [(p, (d, t))].
    
  typeOfSort(s, SortVar(name)) = SRT_VAR(name).
  
rules // rules 

  sectionOk(s, Rules(rs)) = Rules(rs') :- 
    rulesOk(s, rs) == rs'.
  sectionOk(s, Strategies(strs)) = Strategies(strs') :-
    rulesOk(s, strs) == strs'.
  
  ruleOk : scope * Def -> Def
  rulesOk maps ruleOk(*, list(*)) = list(*)
  
  ruleOk(s, RDecl(f, t1, t2)) = RDecl(f, t1, t2) :-
  	s -> Rule{f@f} with typeOfRule FUN([t1], t2). 
   
  ruleOk(s, SDefNoArgs(f, strategy)) = SDefNoArgs(f, strategy) :- {s_r t}
    new s_r, s_r -P-> s,
    typeOfStrategy(s_r, strategy) == t,
    consistent(t, DYN()).
  
  ruleOk(s, SDefTypeCheck(f, c, rules)) = SDefTypeCheck(f, c, rules).
  
  ruleOk(s, RDefNoArgs(f, r)) = def :- {s_r t1 t2 ts sg}
    typeOfRule of Rule{f@f} in s |-> sg,
    resolveRule(s, RDefNoArgs(f, r), sg) == def.    
 
  resolveRule: scope * Def * list((path * (occurrence * TYPE))) -> Def
  
  resolveRule(s, RDefNoArgs(f, Rule(trm1, trm2, cond)), []) = RDefNoArgs(f, Rule(trm1, trm2', cond')) :- {s_r t1 t2}
  	new s_r, s_r -P-> s, 
  	typeOfMatchTerm(s_r, trm1) == t1,
  	resolveRuleMatchTerm(trm1, t1),
    typeOfBuildTerm(s_r, trm2) == (trm2', t2),
    checkConds(s_r, cond) == cond'.
    
  resolveRuleMatchTerm: Term * TYPE
  
  resolveRuleMatchTerm(Var(x), t) :- t == DYN().
  
  resolveRuleMatchTerm(Tuple([trm|trms]), TUP([ty|tys])) :- 
  	resolveRuleMatchTerm(trm, ty),
  	resolveRuleMatchTerm(Tuple(trms), TUP(tys)).
  
  resolveRuleMatchTerm(Tuple([x]), TUP([t])) :- resolveRuleMatchTerm(x, t).
   
  resolveRuleMatchTerm(_ , t) :- t == DYN(). // there will be constraints already for other cases
    
  resolveRule(s, RDefNoArgs(f, Rule(trm1, trm2, cond)), [(_, (_, FUN([t], t')))]) = def :- {s_r t1 t2 trm2' cond'}
  	new s_r, s_r -P-> s,
  	typeOfMatchTerm(s_r, trm1) == t,
    typeOfBuildTerm(s_r, trm2) == (trm2', t'),
    resolveRuleBuildTerm(s_r, trm2', t'), // will set variable in scope graph if necessary
    checkConds(s_r, cond) == cond',
  	def == RDefNoArgs(f, Rule(trm1, trm2', cond')).
  	
  resolveRuleBuildTerm : scope * Term * TYPE
  resolveRuleBuildTerm(s, NoAnnoList(trm), t) :-
  	resolveRuleBuildTerm(s, trm, t).
  	
  resolveRuleBuildTerm(s, Var(x), t) :-
  	s -> Var{x@x} with typeOfVar t.
  	
  resolveRuleBuildTerm(s, term, t).
  	
  	  
  checkCond: scope * RuleCond -> RuleCond
  checkConds maps checkCond(*, list(*)) = list(*)
  
  checkCond(s, WhereClause(str)) = WhereClause(str') :-
  	checkStrategy(s, str) == str'.
  	  	
  checkCond(s, WithClause(str)) = WithClause(str') :-
  	checkStrategy(s, str) == str'.
  
  checkStrategy: scope * Strategy -> Strategy
  	
  checkStrategy(s, Seq(str1,str2)) = Seq(str1', str2') :- {t3}
  	checkStrategy(s, str1) == str1',
  	checkStrategy(s, str2) == str2',
  	typeOfStrategy(s, Seq(str1, str2)) == t3.
  	  
  checkStrategy(s, Assign(Var(x), sth)) = Assign(Var(x), term') :- {trm t ty sg}
  	typeOfBuildTerm(s, sth) == (trm, t),
    typeOfVar of Var{x@x} in s |-> [(_, (_, ty))],
  	//resolveAssignedVariable(s, sg, Var(x), t) == ty,
  	// will cast if we have to
  	cast(trm, t, ty) == term'.
  	//s -> Var{x@x} with typeOfVar t.
  	
  checkStrategy(s, Assign(OfType(Var(x), ty), sth)) = Assign(Var(x), trm') :- {t trm}
  	typeOfBuildTerm(s, sth) == (trm, t),
  	s -> Var{x@x} with typeOfVar ty,
  	cast(trm, t, ty) == trm'.
  	
  resolveAssignedVariable: scope * list((path * (occurrence * TYPE))) * Term * TYPE -> TYPE
  
  // type in the build term (t) has to be the same as defined for var  
  resolveAssignedVariable(s, [(_, (_, t))], Var(x), ty) = t.
 
  resolveAssignedVariable(s, [], Var(x), ty) = ty :-
	// type is set by the build term if var type is imposed already
	s -> Var{x@x} with typeOfVar ty. 
      
  typeOfStrategy: scope * Strategy -> TYPE
  
  typeOfStrategy(s, Seq(str1, str2)) = FUN([t11], t22) :- {t12 t21}
  	typeOfStrategy(s, str1) == FUN([t11], t12),
  	typeOfStrategy(s, str2) == FUN([t21], t22),
  	consistent(t12,t21).
  	
  typeOfStrategy(s, CallNoArgs(SVar(str))) = t :- {p d t'}
  	typeOfRule of Rule{str@str} in s |-> [(p, (d, t'))],
  	t == t'.
  
  typeOfTerm: scope * Term -> TYPE
  
  typeOfStrategy(s, Match(trm)) = FUN([t], DYN()) :-
    typeOfMatchTerm(s, trm) == t.
    
  typeOfStrategy(s, Build(trm)) = FUN([DYN()], t) :- {trm'}
    typeOfBuildTerm(s, trm) == (trm', t).
    
  typeOfStrategy(s, Id()) = FUN([DYN()], DYN()).

rules // MATCH TERM    
  typeOfMatchTerm : scope * Term -> TYPE
  typeOfMatchTerms maps typeOfMatchTerm(*, list(*)) = list(*)
  
  assertTypeOfMatchTerm : scope * Term -> TYPE
  assertTypeOfMatchTerms maps assertTypeOfMatchTerm(*, list(*)) = list(*)
  
  assertTypeOfMatchTerm(s, Var(x)) = t :-
  	s -> Var{x@x} with typeOfVar t.
  	
  assertTypeOfMatchTerm(s, Tuple(trms)) = TUP(ts) :-
  	assertTypeOfMatchTerms(s, trms) == ts.
  	
  assertTypeOfMatchTerm(s, Op(c, trms)) = t :- {p d ts ts'}
  	typeOfCons of Cons{c@c} in s |-> [(p, (d, FUN(ts, t)))],
  	assertTypeOfMatchTerms(s, trms) == ts.
  	
  assertTypeOfMatchTerm(s, OfType(Var(x), t)) = t' :-
    s -> Var{x@x} with typeOfVar t,
    consistent(t, t').
  
  typeOfMatchTerm(s, NoAnnoList(Var(x))) = DYN() :-
    s -> Var{x@x} with typeOfVar DYN().
  
  typeOfMatchTerm(s, NoAnnoList(term)) = t :-
    typeOfMatchTerm(s, term) == t.
  
  typeOfMatchTerm(s, Var(x)) = t :-
   s -> Var{x@x} with typeOfVar t.
   //t == DYN().
   
  /*
  typeOfMatchTerm(s, OfType(Var(x), t)) = t :- {s_t t'}
    //new s_t, s_t -P-> s,
    s -> Var{x@x} with typeOfVar t,
    typeOfMatchTerm(s_t, Var(x)) == t',
    consistent(t, t').
  */
  
  typeOfMatchTerm(s, Op(c, trms)) = t :- {p d ts ts'}
    typeOfCons of Cons{c@c} in s |-> [(p, (d, FUN(ts, t)))],
    //typeOfMatchTerms(s, trms) == ts', consistentL(ts, ts').
    assertTypeOfMatchTerms(s, trms) == ts.
   
  typeOfMatchTerm(s, Tuple(trms)) = TUP(ts) :- 
  	typeOfMatchTerms(s, trms) == ts.
  	
  typeOfMatchTerm(s, Int(val)) = INT().
  typeOfMatchTerm(s, Str(val)) = STRING().
  typeOfMatchTerm(s, Real(val)) = REAL().
  
  typeOfMatchTerm(s, ListTail(heads, tail)) = LIST(t) :- {ts}
    typeOfMatchTerms(s, heads) == ts,
    typeOfMatchTerm(s, tail) == t,
    allEqual(s, ts).
    
  	
rules  // BUILD TERMS	
  typeOfBuildTerm : scope * Term -> (Term * TYPE)
  typeOfBuildTerms: scope * list(Term) -> (list(Term) * list(TYPE))
  
  typeOfBuildTerms(s, [term]) = ([trm], [ty]) :-
  	typeOfBuildTerm(s, term) == (trm, ty).
  	
  typeOfBuildTerms(s, [hd|terms]) = ([hd'|terms'], [t|types]) :-
    typeOfBuildTerm(s, hd) == (hd', t),
    typeOfBuildTerms(s, terms) == (terms', types).
  
  typeOfBuildTerm(s, NoAnnoList(term)) = (NoAnnoList(term'), t) :-
    typeOfBuildTerm(s, term) == (term', t).
  
  typeOfBuildTerm(s, Var(x)) = (Var(x), t) :- 
   	typeOfVar of Var{x@x} in s |-> [(_, (_, t))].
  
  typeOfBuildTerm(s, Tuple(terms)) = (Tuple(terms'), TUP(ts)) :- 
  	typeOfBuildTerms(s, terms) == (terms', ts).
    
  typeOfBuildTerm(s, App(CallNoArgs(SVar(str)), term)) = (App(CallNoArgs(SVar(str)), trm), t') :- {sg ty term' t}
  	// if type of rule is (DYN -> sth) then casting will be handled inside of rule
  	// if type of rule isnt (DYN -> sth) then casting is handled on application site
  	typeOfRule of Rule{str@str} in s |-> sg,
  	typeOfBuildTerm(s, term) == (term', ty),
	handleRuleDeclApp(sg, term', ty) == (trm, t'). // handles insertion of casts

  handleRuleDeclApp : list((path * (occurrence * TYPE))) * Term * TYPE -> (Term * TYPE)
  
  handleRuleDeclApp([], term, termType) = (term, DYN()).
  handleRuleDeclApp([(_, (_, FUN([ts], t)))], term, termType) = (term', t) :-
  	cast(term, termType, ts) == term'.
 
 // use separate rule to handle situations where type is not obtained from scope-graph
 // e.g. when resolving strategies
  handleRuleApp : Term * TYPE * TYPE -> (Term * TYPE)
  
  // if they're already cast, then just check if they're consistent with the rule being applied on them
  handleRuleApp(CastTerm(term, ty1, ty2), termType, returnType) = (CastTerm(term, ty1, ty2), returnType) :-
  	consistent(returnType, ty2).
   
  handleRuleApp(OfType(term, ty), termType, returnType) = (OfType(term, ty), returnType) :-
  	consistent(returnType, ty).
  	
  // if they're not cast yet, insert a cast if required
  handleRuleApp(term, termType, returnType) = (term', returnType) :-
  	cast(term, termType, returnType) == term'.
  	
  typeOfBuildTerm(s, App(str, term)) = (App(str, trm), t) :- {ts t' term'} 
  // we support Seq... others will suffer
  	typeOfStrategy(s, str) == FUN(ts, t),
  	typeOfBuildTerm(s, term) == (term', t'),
  	handleRuleApp(term', t', t) == (trm, t).
  	
  typeOfBuildTerm(s, Op(c, trms)) = (Op(c, trms'), t) :- {p d ts}
    typeOfCons of Cons{c@c} in s |-> [(p, (d, FUN(ts, t)))],
    typeOfBuildConsTerms(s, trms) == (trms', ts).
   
  /* 
  typeOfBuildTerm(s, OfType(Op(c, trms), ty)) = (OfType(Op(c, trms'), ty), ty) :- {ts t term}
    typeOfCons of Cons{c@c} in s |-> [(_, (_, FUN(ts, t)))],
    typeOfBuildConsTerms(s, trms) == (trms', ts),
    cast(Op(c, trms), t, ty) == term.
  */
    
  typeOfBuildTerm(s, OfType(term, ty)) = (OfType(term, ty), ty) :- {ty' term'}
    typeOfBuildTerm(s, term) == (term', ty'), 
    consistent(ty', ty). // check if there is conflicting type information - do we need this?
  
  typeOfBuildTerm(s, OpQ(c, trms)) = (OpQ(c, trms'), t) :- {p d ts}
    typeOfCons of Cons{c@c} in s |-> [(p, (d, FUN(ts, t)))],
    typeOfBuildConsTerms(s, trms) == (trms', ts).
    
  typeOfBuildTerm(s, CastTerm(term, t1, t2)) = (CastTerm(term, t1, t2), t2) :- {term'}
  	typeOfBuildTerm(s, term) == (term', t1),
  	consistent(t1, t2).  // Silence CastTerm
  
  typeOfBuildTerm(s, Int(val)) = (Int(val), INT()).
  typeOfBuildTerm(s, Real(val)) = (Real(val), REAL()).
  typeOfBuildTerm(s, Str(val)) = (Str(val), STRING()).
  
  // BUILD TERMS WITHIN CONSTRUCTORS
  typeOfBuildConsTerm: scope * Term -> (Term * TYPE)
  typeOfBuildConsTerms: scope * list(Term) -> (list(Term) * list(TYPE))
  
  typeOfBuildConsTerm(s, Var(x)) = (trm, t) :- {p d t'}
  	typeOfVar of Var{x@x} in s |-> [(p, (d, t'))],
   	cast(Var(x), t', t) == trm.
   	
  typeOfBuildConsTerm(s, OfType(Var(x), t)) = (trm, t) :- {p d t'}
  	typeOfVar of Var{x@x} in s |-> [(p, (d, t'))],
   	cast(Var(x), t', t) == trm.
  
  typeOfBuildConsTerm(s, term) = typeOfBuildTerm(s, term).
  
  typeOfBuildConsTerms(s, [term]) = ([trm], [ty]) :-
  	typeOfBuildConsTerm(s, term) == (trm, ty).
  	
  typeOfBuildConsTerms(s, [hd|terms]) = ([hd'|terms'], [t|types]) :-
    typeOfBuildConsTerm(s, hd) == (hd', t),
    typeOfBuildConsTerms(s, terms) == (terms', types).
  
rules // insertTypeChecks

  insertTypeCheckSection : Section -> Section
  insertTypeCheckSections maps insertTypeCheckSection(list(*)) = *
  
  insertTypeCheckSection(Signature(sigSec)) = Strategies([castVal|rs]) :-
    castVal == CastValStrat(),
    insertTypeCheckSigSections(sigSec) == rs.
  
  insertTypeCheckSection(Rules(rs)) = nil.
  
  insertTypeCheckSigSection : SigSection -> list(Def)
  insertTypeCheckSigSections maps insertTypeCheckSigSection(list(*)) = *
  
  insertTypeCheckSigSection(Sorts(sorts)) = nil.
  insertTypeCheckSigSection(Constructors(cons)) = def :-
    insertTypeCheckCons(cons) == def.
    
  insertTypeCheckCon : OpDecl -> Def
  insertTypeCheckCons maps insertTypeCheckCon(list(*)) = list(*)
  
  insertTypeCheckCon(OpDecl(c, FunType(srts, ConstType(SortNoArgs(typ))))) = SDefTypeCheck(typ, c, srts).
  
  
rules // util

  allEqual : scope * list(TYPE)
  
  allEqual(s, [el]).
  
  allEqual(s, [hd1|[hd2|tail]]) :-
    hd1 == hd2.
