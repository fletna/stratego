module elaborate

imports
  signatures/stratego/-
  signatures/stratego/sugar/-
  signatures/stratego/core/-
  analysis
  statixruntime
  pp
  pp/-
  
  signature
  constructors 
    SDefTypeCheck : ID * ID * List(Type) -> Def
    CastValStrat : Strategy

rules // insert casts

  insert-casts = insert-casts(|"")
  
  insert-casts(|path) :
      ast -> ast'
    where 
      indexed-ast := <stx-index-ast(|path)> ast
    ; ast' := <stx-evaluate(|"static-semantics", "insertCasts")> [indexed-ast]

rules // insert type checks

  insert-typechecks(|path) :
      ast -> ast''
    where
      indexed-ast := <stx-index-ast(|path)> ast
    ; ast' := <stx-evaluate(|"static-semantics", "insertTypeChecks")> [indexed-ast]
    ; ast'' := <transform-typechecks> ast'

  transform-typechecks = topdown(try(transform-typecheck))
  
  transform-typecheck :
  SDefTypeCheck(typ, name, args) -> SDefNoArgs(typ', Match(NoAnnoList(Op(name, args'))))
    where
      typ' := <conc-strings> ("strategoGradual--is-a-", typ)
    ; args' := <map(insert-typecheck-arg)> args

  insert-typecheck-arg = !Wld()
  
  transform-typecheck: 
      CastValStrat() -> SDefT(
          "cast-value"
        , [DefaultVarDec("s")]
        , [DefaultVarDec("typ")]
        , LChoice(
            CallNoArgs(SVar("s"))
          , Seq(
            Match(Var("a")), 
            CallT(
                SVar("report-failure")
              , []
              , [ App(
                    CallNoArgs(SVar("concat-strings"))
                  , NoAnnoList(
                      List(
                        [NoAnnoList(Str("\"Cast to \"")), Var("typ"), NoAnnoList(Str("\" failed\""))]
                      )
                    )
                  ),
                  Var("a")
                ]
              )
            )
          )
        )

  remove-rule-hints = topdown(try(remove-rule-hint))
  remove-rule-hint : Strategies(rs) -> Strategies(<filter(not(?RDecl(_, _, _)))> rs)
  remove-rule-hint : Rules(rs) -> Rules(<filter(not(?RDecl(_, _, _)))> rs)

rules // cast terms

  transform-cast-terms = topdown(try(transform-cast-term))
  
  transform-cast-term :
      CastTerm(e, _, SRT(t)) ->App(
        CallT(SVar("cast-value"), [CallNoArgs(SVar(name))], [NoAnnoList(Str(nameQ))])
      , e
      )
    where
      name := <conc-strings> ("strategoGradual--is-a-", t)
    ; nameQ := <concat-strings> ["\"", t, "\""]
    
  transform-cast-term :
    OfType(e, SRT(t)) -> App(
      CallT(SVar("cast-value"), [CallNoArgs(SVar(name))], [NoAnnoList(Str(nameQ))])
    , e
    )
  where
    name := <conc-strings> ("strategoGradual--is-a-", t)
  ; nameQ := <concat-strings> ["\"", t, "\""]
  

rules // menu items

  menu-source-elaborate:
      (source-ast, _, _, path, project-path) -> (filename, elaborated-ast)
    with
      filename := <guarantee-extension(|"elaborated.aterm")> path
    ; analyzed-ast := <insert-casts(|path);insert-typechecks(|path)> source-ast
    ; elaborated-ast := <strip-annos> analyzed-ast
 
  menu-source-insert-casts-pp:
      (source-ast, _, _, path, project-path) -> (filename, pretty-printed)
    with
      filename := <guarantee-extension(|"elaborated.strg")> path
    ; transformed-ast := <insert-casts(|path)> source-ast
    ; pretty-printed := <pp-StrategoGradual-string> transformed-ast
    
  menu-source-transform-casts-pp:
      (source-ast, _, _, path, project-path) -> (filename, pretty-printed)
    with
      filename := <guarantee-extension(|"cst.strg")> path
    ; transformed-ast := <transform-cast-terms> source-ast
    ; pretty-printed := <pp-StrategoGradual-string> transformed-ast
    
  menu-source-insert-typechecks-pp:
      (source-ast, _, _, path, project-path) -> (filename, pretty-printed)
    with
      filename := <guarantee-extension(|"tchk.strg")> path
    ; transformed-ast := <insert-typechecks(|path)> source-ast
    ; pretty-printed := <pp-StrategoGradual-string> transformed-ast
    
  menu-source-elaborate-pp:
      (source-ast, _, _, path, project-path) -> (filename, pretty-printed)
    with
      filename := <guarantee-extension(|"el.str")> path
    ; transformed-ast := <insert-casts(|path);insert-typechecks(|path);transform-cast-terms;remove-rule-hints> source-ast
    ; pretty-printed := <pp-StrategoGradual-string> transformed-ast
