module test-inline
 
language Stratego-Sugar

/**
 * Basic inline testcases: errors do not occur
 */
setup [[
  module test-inline
  rules
    aaa = fail
    bbb = fail
]]

test Inline Call Selected
  [[ 
  	  bar = [[foo]]
  	  foo = aaa 
  ]] refactor inline-refactoring to
  [[ 
  	  bar = aaa
  ]]  

test Inline Multiple Calls
  [[ 
  	  bar1 = [[foo]]
  	  bar2 = [[foo]]
  	  foo = aaa 
  ]] refactor inline-refactoring to
  [[ 
  	  bar1 = aaa
  	  bar2 = aaa
  ]]  

test Inline Definition Selected
  [[ 
  	  bar = foo
  	  [[foo = aaa]] 
  ]] refactor inline-refactoring to
  [[ 
  	  bar = aaa
  ]]  

test Inline Simple Rule
  [[ 
  	  bar = <map(foo)> [1,2,3]
  	  [[foo: a -> (a,a)]] 
  ]] refactor inline-refactoring to
  [[ 
  	  bar = <map(\a -> (a,a)\)> [1,2,3]
  ]]  

test Inline Conditional Rule with Variabeles
  [[ 
  	  bar = <map(foo)> [1,2,3]
  	  [[foo: a -> (a,b) where b := <aaa> a ]] 
  ]] refactor inline-refactoring to
  [[ 
  	  bar = <map({b,a: (a -> (a,b) where b := <aaa> a)})> [1,2,3]
  ]]  

test Inline Strategy with Variabeles
  [[ 
  	  bar = <map(foo)> [1,2,3]
  	  [[foo = ?a; !(a,a)]] 
  ]] refactor inline-refactoring to
  [[ 
  	  bar = <map({a: ?a; !(a,a)})> [1,2,3]
  ]]  


     