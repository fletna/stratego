module test-rename-simple
 
language Stratego-Sugar

/**
 * Basic rename testcases: basic constructs, errors do not occur
 */
setup [[
  module test-rename-simple
  rules
]]

test Rename Term-Variabele
  [[ 
      bar(|[[t]]) =  <id> t 
  ]] refactor rename-refactoring("foo") to 
  [[
      bar(|foo) = <id> foo
  ]]

test Rename Local-Declared-Variabele
  [[ 
      r: (x,y) -> z
      where 
      	[[z]] := <subt> (x,y) 
  ]] refactor rename-refactoring("foo") to 
  [[
      r: (x,y) -> foo
      where 
      	foo := <subt> (x,y) 
  ]]

test Rename Strategy-Variabele
  [[ 
      bar([[s]]) =  id; s 
  ]] refactor rename-refactoring("foo") to 
  [[
      bar(foo) = id ; foo
  ]]

test Rename Call-To-Strategy-Variabele 
  [[ 
  	  s = fail
      bar(s) =  id; [[s]] 
  ]] refactor rename-refactoring("foo") to 
  [[
  	  s = fail
      bar(foo) = id ; foo
  ]]

test Rename Call-To-Strategy-Variabele-Multi-Args 
  [[ 
  	  s(s1|t1,t2) = fail
      bar([[s]]) =  id; s(id|"x", "y") 
  ]] refactor rename-refactoring("foo") to 
  [[
  	  s(s1|t1,t2) = fail
      bar(foo) = id ; foo(id|"x", "y")
  ]]
  
test Rename Global Strategy 
  [[ 
  	  aaa = bar("aaa")
  	  bar(x): a -> a
      [[bar]](s) =  id; s 
  ]] refactor rename-refactoring("foo") to 
  [[
  	  aaa = foo("aaa")
  	  foo(x): a -> a
      foo(s) = id ; s
  ]]

test Rename Let (1)
  [[ 
  	  bbb(s|n) = <s> n
      aaa(s) =
        let [[apply]](|i) = <s> (i, <id>)
          in bbb(apply | 1)
        end
  ]] refactor rename-refactoring("foo") to 
  [[
  	  bbb(s|n) = <s> n
      aaa(s) =
        let foo(|i) = <s> (i, <id>)
          in bbb(foo | 1)
        end
  ]]

test Rename Let (2)
  [[ 
  	  bbb(s|n) = <s> n
      aaa(s) =
        let apply(|i) = <s> (i, <id>)
          in [[bbb]](apply | 1)
        end
  ]] refactor rename-refactoring("foo") to 
  [[
  	  foo(s|n) = <s> n
      aaa(s) =
        let apply(|i) = <s> (i, <id>)
          in foo(apply | 1)
        end
  ]]

test Rename Recursion
  [[ 
    mapp(s) = rec [[x]]([] + [s | x])
  ]] refactor rename-refactoring("foo") to 
  [[
    mapp(s) = rec foo([] + [s | foo])
  ]]

    
test Rename Signature refactoring 
  [[ 
      r : [[Bar]](x,y) -> Bar(y,x) 
      signature constructors
        Bar : Id * Id -> Bar
  ]] refactor rename-refactoring("Foo") to 
  [[
      r : [[Foo]](x,y) -> Foo(y,x) 
      signature constructors
        Foo : Id * Id -> Bar
  ]]

