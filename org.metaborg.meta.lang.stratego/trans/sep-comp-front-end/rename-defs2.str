module sep-comp-front-end/rename-defs2

imports libstrc
imports libspoofax/term/annotation

signature constructors

  LocalArg : Anno

strategies

  // Rename the names of all top-level strategy definitions; 
  // The Stratego syntax cannot be used for names of Java classes.

  rename-calls2 =
    topdown(try(RenameCall2))

rules

  RenameSDef2 :
    AnnoDef(a*, 'def) -> AnnoDef(a*, <RenameSDef2> 'def)

  RenameSDef2 : 
    SDefT(x, args1, args2, s) -> SDefT(x, args1, args2, s')
    where <length> args1 => n
        ; <length> args2 => m
        ; s' := <annotate-local-args(!$[[x]$[<id>]]|<map(?VarDec(<id>,_))> args1, [])> s

  RenameSDef2 :
    ExtSDef(_, _, _) -> <id>

  RenameSDef2 : 
    ExtSDefInl(x, args1, args2, s) -> ExtSDefInl(x, args1, args2, s')
    where <length> args1 => n
        ; <length> args2 => m
        ; s' := <annotate-local-args(!$[[x]$[<id>]]|<map(?VarDec(<id>,_))> args1, [])> s

  RenameCall2 : 
    CallT(SVar(x), ss, ts) -> CallT(SVar(y), ss, ts)
    where id
      ; <get-annotation(?LocalArg())> x
      ; y := <remove-annotations(?LocalArg())> x

  RenameCall2 : 
    CallT(SVar(x), ss, ts) -> CallT(SVar(y), ss, ts)
    where id
      ; not(<get-annotation(?LocalArg())> x)
      ; <length> ss => n
      ; <length> ts => m
      ; <rename-var-manual>(x, (n,m)) => y


  rename-var-manual:
    (x,(n,m)) -> <concat-strings>[<cify> x, "_", <int-to-string> n, "_", <int-to-string> m]

  annotate-local-args(rename-local|larg*,letdef*) = alltd(preserve-annos(annotate-local-arg(rename-local|larg*,letdef*)))

  annotate-local-arg(rename-local|larg*,letdef*): Let(sdeft*, body) -> Let(sdeft'*, body')
  with id
    ; def* := <map(?SDefT(<id>, _, _, _);split(rm-annotations, rename-local))> sdeft*
    ; letdef'* := [def*,letdef*]
    ; body' := <annotate-local-args(rename-local|larg*, letdef'*)> body
    ; sdeft'* := <map({args1, arg*:
        SDefT(rename-local, ?args1, id, id)
      ; where(arg* := <map(?VarDec(<id>,_))> args1)
      ; SDefT(id, id, id, annotate-local-args(rename-local|[larg*, arg*], letdef'*))})> sdeft*

  annotate-local-arg(rename-local|larg*,letdef*): CallT(SVar(x), ss, ts) -> CallT(SVar(y), ss, ts)
  where id
    ; <getfirst(?x)> larg*
    ; y := <add-annotation(|LocalArg())> x

  annotate-local-arg(rename-local|larg*,letdef*): CallT(SVar(x), ss, ts) -> CallT(SVar(y), ss, ts)
  where id
    ; y := <lookup;add-annotation(|LocalArg())> (x{}, letdef*)
